package lodzjug.presentation.drools.example01.rules;

//list any import classes here.
import lodzjug.presentation.drools.example01.model.ChestPain;
import lodzjug.presentation.drools.example01.model.Question;
import lodzjug.presentation.drools.example01.model.Symptom;
import lodzjug.presentation.drools.example01.model.PainAtRest;
import lodzjug.presentation.drools.example01.model.Age;
import lodzjug.presentation.drools.example01.model.StSegmentAbnormal;
import lodzjug.presentation.drools.example01.model.TWaveAbnormal;
import lodzjug.presentation.drools.example01.model.PulmonaryEdema;
import lodzjug.presentation.drools.example01.model.Diagnose;
import lodzjug.presentation.drools.example01.model.Cad;
import lodzjug.presentation.drools.example01.model.Unknown;
import org.slf4j.Logger;
import org.slf4j.LoggerFactory;


//declare any global variables here

global Logger logger;

rule "Chest pain"

	salience 1000
    
    when
        chestPain : ChestPain( existing == true)
    then
        insert(new Question(PainAtRest.class));
        insert(new Question(Age.class));
        insert(new Question(StSegmentAbnormal.class));
        insert(new Question(TWaveAbnormal.class));
        insert(new Question(PulmonaryEdema.class));
        
        logger.info("Ouch! Chest pain. Let's ask to see if CAD risk is high");
end

rule "Check if CAD risk is high"
	salience 1000
    when
        painAtRest : PainAtRest( durationInMinutes >= 20, existing == true )
        age : Age( ageInYears >= 65, existing == true )
        stSegmentAbnormal : StSegmentAbnormal( existing == true )
        tWaveAbnormal : TWaveAbnormal( existing == true )
        pulmonaryEdema : PulmonaryEdema( existing == true )
    then
        insert(new Cad());
        logger.info("Oooops!  CAD risk is high, better take care of yourself");
end

rule "What if no CAD diagnosed?"
	salience 100
    when
        not Diagnose(  )
        not Question( answered == false )
    then
        insert(new Unknown());
        logger.info("Phew! CAD risk is low, but we still don't know what's wrong.");
end
